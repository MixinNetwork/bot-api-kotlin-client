plugins {
    id 'java-library'
    id 'kotlin'
    id 'maven-publish'
    id 'java'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

configurations {
    ktlint
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "org.bouncycastle:bcprov-jdk15on:$bouncycastleVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "com.github.MixinNetwork.jjwt:jjwt-api:$jjwtVersion"
    api "com.github.MixinNetwork.jjwt:jjwt-impl:$jjwtVersion"
    api "com.github.MixinNetwork.jjwt:jjwt-gson:$jjwtVersion"
    api "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:${coroutineAdapterVersion}"
    implementation "net.i2p.crypto:eddsa:$ed25519Version"
    implementation "org.whispersystems:curve25519-java:$curve25519Version"
    ktlint "com.pinterest:ktlint:$ktlintVersion"

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // to add a baseline to check against prepend following args:
    // "--baseline=ktlint-baseline.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}

sourceCompatibility = 1.8
publishing {
    publications {
        customLibrary(MavenPublication) {
            groupId = 'com.github.MixinNetwork'
            artifactId = 'bot-api-kotlin-client'
            version = 'v0.5.7'

            from components.java

            pom {
                name = 'bot-api-kotlin-client'
                description = 'The Mixin Network based wallet allows for the rapid construction of decentralized wallets, decentralized on-chain exchanges, and other products.'
                url = 'https://github.com/MixinNetwork/bot-api-kotlin-client/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }

            pom.withXml {
                asNode()
                        .dependencies
                        .dependency
                        .findAll { dependency ->
                            // find all dependencies with runtime scope
                            dependency.scope.text() == 'runtime'
                        }
                        .each { dependency ->
                            // set the scope to 'compile'
                            dependency.scope*.value = 'compile'
                        }
            }
        }
    }

    repositories {
        mavenLocal()
        maven {
            name = 'bot-api-kotlin-client'
            url = layout.buildDirectory.dir("repo")
        }
    }
}